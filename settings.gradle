plugins {
  id 'com.gradle.develocity' version '3.18.1'
  id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.2'
}

def isCI = System.getenv('CI') != null
def isLocal = !isCI
def isAuthenticated = System.getenv('DEVELOCITY_ACCESS_KEY') != null
def isBuildCacheAuthenticated =
  System.getenv('DEVELOCITY_BUILD_CACHE_NODE_USER') != null &&
    System.getenv('DEVELOCITY_BUILD_CACHE_NODE_KEY') != null

develocity {
  server = 'https://ge.grails.org'
  buildScan {
    tag('grails-plugins')
    tag('grails-plugins/grails-audit-logging')
    publishing.onlyIf { isAuthenticated }
    uploadInBackground = isLocal
  }
}

buildCache {
  local { enabled = isLocal }
  remote(develocity.buildCache) {
    push = isCI && isBuildCacheAuthenticated
    enabled = true
    usernameAndPassword(
      System.getenv('DEVELOCITY_BUILD_CACHE_NODE_USER') ?: '',
      System.getenv('DEVELOCITY_BUILD_CACHE_NODE_KEY') ?: ''
    )
  }
}

rootProject.name = 'grails-audit-logging'

include 'plugin'
include 'examples:audit-test'
include 'examples:audit-test-allow-update-outside-transaction'

findProject(":plugin").name = "audit-logging"
